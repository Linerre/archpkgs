# Maintainer: Errenil Noel <errelinaaron@gmail.com>

_base=meow
pkgname=${_base}-git
pkgver=1.4.5.59c058c
pkgrel=1
pkgdesc="Yet another modal editing on Emacs"
arch=('any')
url="https://github.com/meow-edit/${_base}"
license=('GPL')
# groups=()
depends=('emacs')
provides=(${_base})
conflicts=(${_base})
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_base}"
    local pkgv=1.4.5
    printf "${pkgv}.%s" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_base}"
    emacs -Q -L . --batch -f batch-byte-compile *.el
}

package() {
    cd "${srcdir}/${_base}"
    local dest="${pkgdir}"/usr/share/emacs/site-lisp/"${_base}"

    # install -d create the dir for to-be-installed files
    install -d "${dest}"
    # install -D actually cp the to-be-installed files to the dir above
    install -Dm644 *.{el,elc} "${dest}"
    install -Dm644 *.org "${dest}"
}
